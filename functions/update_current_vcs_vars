unset __CURRENT_GIT_BRANCH
unset __CURRENT_GIT_BRANCH_STATUS
unset __CURRENT_GIT_BRANCH_IS_DIRTY

unset __CURRENT_HG_BRANCH
unset __CURRENT_HG_IS_TIP
unset __CURRENT_HG_BRANCH_IS_DIRTY

local st="$(git status 2>/dev/null)"
if [[ -n "$st" ]]; then
    local -a arr
    arr=(${(f)st})

    if [[ $arr[1] =~ 'Not currently on any branch.' ]]; then
        __CURRENT_GIT_BRANCH='no-branch'
    else
        __CURRENT_GIT_BRANCH="${arr[1][(w)4]}";
    fi

    if [[ $arr[2] =~ 'Your branch is' ]]; then
        if [[ $arr[2] =~ 'ahead' ]]; then
            __CURRENT_GIT_BRANCH_STATUS='ahead'
        elif [[ $arr[2] =~ 'diverged' ]]; then
            __CURRENT_GIT_BRANCH_STATUS='diverged'
        else
            __CURRENT_GIT_BRANCH_STATUS='behind'
        fi
    fi

    if [[ ! $st =~ 'nothing to commit' ]]; then
        __CURRENT_GIT_BRANCH_IS_DIRTY='1'
    fi
fi

local hg_br="$(hg id -nb 2>/dev/null)"
if [[ -n "$hg_br" ]]; then
    __CURRENT_HG_BRANCH="$hg_br[(w)2]"

    if [ "$hg_br[(w)1]" "!=" $(hg id --debug -i -r tip) ]; then
        __CURRENT_HG_IS_TIP=1
    fi

    if [[ -n "$(hg status -q)" ]]; then
        __CURRENT_HG_BRANCH_IS_DIRTY=1
    fi
fi
